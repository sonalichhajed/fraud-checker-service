plugins {
    id 'com.palantir.docker' version '0.26.0'
    id 'com.palantir.docker-run' version '0.26.0'
    id 'org.springframework.boot' version '2.4.0-M3'
    id "io.spring.dependency-management" version "1.0.11.RELEASE"
    id 'java'
    id 'idea'
    id 'eclipse'
}

group = 'com.tsys'
version = '1.0.0'
sourceCompatibility = '11'

repositories {
    mavenCentral()
    maven { url 'https://repo.spring.io/milestone' }
//	maven {
//		url "https://plugins.gradle.org/m2/"
//	}
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    // For Spring Boot projects this dependency is good enough for Swagger
    implementation 'io.springfox:springfox-boot-starter:3.0.0'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'io.projectreactor:reactor-test'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.4.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.4.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.4.2'
}

test {
    useJUnitPlatform()
}

task taggedTest(type: Test) {
    def incTags = System.getProperty("includeTags") ?
            System.getProperty("includeTags") : 'UnitTest'
    def excTags = System.getProperty("excludeTags") ?
            System.getProperty("excludeTags") : 'ComponentTest & End-To-End-Test'

    println '*'
    println "* Running tests with Tags = $incTags and exclude tests with Tags = $excTags"
    println '*'

    useJUnitPlatform {
        includeTags  incTags
        excludeTags  excTags
    }
    reports {
        junitXml.enabled true
        junitXml.destination(file("${testResultsDir}/test"))
        binaryResultsDirectory = file("${testResultsDir}/test/binary")
        html.enabled true
        html.destination(file("$testReportDir/test"))
    }
}

// To reload on changing a static file, we need a way to copy
// the changed files into the classpath of the running app.
task reload(type: Copy) {
    from 'src/main/resources'
    into 'build/resources/main'
    include 'static/**'
    include 'templates/**'
    include 'custom/**'
}

// To pass arguments while running our application using Gradle
// Plugin, we need to configure our bootRun task:
// Usage on command line:
// gradle bootRun -Dserver.port=10001 -DcustomArgument=custom
bootRun {
    systemProperties = System.properties
}

docker {
    def dockerImageName = "dhavaldalal/fraud-checker-service"
    name "$dockerImageName:$version"
    dockerfile file('Dockerfile')
    // To directly reference the Jar file in the Dockerfile using ARG JAR_FILE
    def artifact = "${project.name}-${project.version}.jar"
    // copies artifact + Dockerfile to the build context
    files "${libsDir}/$artifact", "$projectDir/docker/$dockerImageName/Dockerfile"

    //
    // ├── build
    // │   ├── classes
    // │   │   └── java
    // │   ├── docker <<<----- This is the working dir for docker task ----->>>
    // │   │   └── Dockerfile
    // │   ├── generated
    // │   │   └── sources
    // │   ├── libs
    // │   │   └── fraud_checker-1.0.0.jar
    //

    // passing in the jar file location via --build-arg key=value
    buildArgs([JAR_FILE: artifact, BUILD_VERSION: "$version"])
    pull false
    noCache true
}

dockerRun {
    name 'fraud_checker'
    image "dhavaldalal/fraud-checker-service:$version"
//    volumes 'hostvolume': '/containervolume'
    ports '9001:9001'
    daemonize false
    clean true
//    env 'MYVAR1': 'MYVALUE1', 'MYVAR2': 'MYVALUE2'
}